group 'de.heisluft.annotation'
version '1.0.1'

description 'An extended, Java 9 compatible jsr305 implementation'

apply plugin: 'java'
apply plugin: 'signing'
apply plugin: 'maven-publish'

sourceCompatibility = targetCompatibility = 1.8

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

signing {
    sign publishing.publications.testing
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    def login = file("credentials").text.split(":", 2)
    if (login == null || login.length != 2) return
    repositories {
        maven {
            url "http://heisluft.bplaced.net/upload/"
            credentials {
                username = login[0]
                password = login[1]
            }
            authentication {
                basic(BasicAuthentication)
            }
        }
    }
    publications {
        testing(MavenPublication) {
            pom {
                licenses {
                    license {
                        name = 'GNU AGPL, Version 3'
                        url = 'https://www.gnu.org/licenses/agpl.txt'
                    }
                }
                developers {
                    developer {
                        id = 'heisluft'
                        name = 'heisluft'
                        email = 'heisluftlp@gmail.com'
                        roles = ['developer']
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/heisluft/jsr305-extended'
                    developerConnection = 'scm:git:https://github.com/heisluft/jsr305-extended'
                    url = 'https://github.com/heisluft/jsr305-extended'
                }
            }
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

publish.dependsOn jar, sourcesJar, javadocJar